&НаКлиенте
Процедура ИзменитьЦену(Команда)
	Если Объект.Ссылка.Пустая() Тогда //1
		Сообщить("Перед установкой цены необходимо записать документ!");
	Иначе
		ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаСозданияЦены",Новый Структура("Номенклатура", Объект.Ссылка),,,,,
		Новый ОписаниеОповещения("ПослеИзмененияЦены", ЭтаФорма),РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс); //2
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеИзмененияЦены(Результат, ДополнительныеПараметры) Экспорт
	ТекущаяРозничнаяЦена =  РаботаСЦенами.ПолучитьЦенуПродажиНаДату(Объект.Ссылка);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ Объект.Ссылка.Пустая() Тогда
		ТекущаяРозничнаяЦена = РаботаСЦенами.ПолучитьЦенуПродажиНаДату(Объект.Ссылка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТипНоменклатурыПриИзменении(Элемент)
	УстановитьВидимость()
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимость()
	Если Объект.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипНоменклатуры.Услуга") Тогда
		Элементы.ДлительностьУслуги.Видимость = Истина;
		Элементы.СрокГодностиВДнях.Видимость = Ложь;
	Иначе
		Элементы.ДлительностьУслуги.Видимость = Ложь;
		Элементы.СрокГодностиВДнях.Видимость = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура СчетБухгалтерскогоУчетаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;       
	СписокДоступныхЗначений = Новый СписокЗначений;    
	Если Объект.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипНоменклатуры.Товар") Тогда
		СписокДоступныхЗначений.Добавить(ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.Материалы"));
		СписокДоступныхЗначений.Добавить(ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.Товары"));
	ИначеЕсли  Объект.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипНоменклатуры.Услуга") Тогда
		СписокДоступныхЗначений.Добавить(ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.РасходыНаПродажу"));
		СписокДоступныхЗначений.Добавить(ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ПрочиеДоходыИРасходы"));
	Иначе
		СписокДоступныхЗначений.Добавить(ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ПустаяСсылка"));
	КонецЕсли;    
	ДанныеВыбора = СписокДоступныхЗначений;
КонецПроцедуры

