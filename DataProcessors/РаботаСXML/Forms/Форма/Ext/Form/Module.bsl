&НаКлиенте
Процедура ЗаписатьВXMLПоследовательно(Команда)
	ИмяФайла = Каталог + Формат(ДатаВыгрузки, "ДФ=dd.MM.yy") + ".xml"; 
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяФайла);
	ЗаписьXML.ЗаписатьОбъявлениеXML(); 
	ЗаписьXML.ЗаписатьНачалоЭлемента("КорневойУзел");
	ЗаписьXML.ЗаписатьКомментарий("Выгрузка реализаций за дату");
	ДанныеРеализации = ПолучитьДанныеОРеализацияхЗаДатуЦеликом();
	Для ТекущаяСтрока = 0 По ДанныеРеализации.Количество() - 1 Цикл
		Данные = СтрРазделить(ДанныеРеализации[ТекущаяСтрока], ";");
		ЗаписьXML.ЗаписатьНачалоЭлемента("Реализация");
		ЗаписьXML.ЗаписатьАтрибут("Номер", Данные[4]);
		ЗаписьXML.ЗаписатьНачалоЭлемента("Сотрудник");
		ЗаписьXML.ЗаписатьТекст(Данные[0]);
		ЗаписьXML.ЗаписатьКонецЭлемента();
		ЗаписьXML.ЗаписатьНачалоЭлемента("СуммаДокумента");
		ЗаписьXML.ЗаписатьТекст(Данные[1]);
		ЗаписьXML.ЗаписатьКонецЭлемента();
		ЗаписьXML.ЗаписатьНачалоЭлемента("Оплата");
		ЗаписьXML.ЗаписатьТекст(Данные[2]);
		ЗаписьXML.ЗаписатьКонецЭлемента();
		ЗаписьXML.ЗаписатьНачалоЭлемента("Дата");
		ЗаписьXML.ЗаписатьТекст(Данные[3]);
		ЗаписьXML.ЗаписатьКонецЭлемента();
		ЗаписьXML.ЗаписатьКонецЭлемента();
	КонецЦикла;
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.Закрыть();
	ОбщегоНазначенияКлиент.СообщитьПользователю("Выгрузка завершена!");
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьИзXMLПоследовательно(Команда)
	ИмяФайла = Каталог + Формат(ДатаВыгрузки, "ДФ=dd.MM.yy") + ".xml";
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ИмяФайла);
	Пока ЧтениеXML.Прочитать() Цикл
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ЧтениеXML.Имя);
			Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
				ТекстАтрибута = "Атрибут: " + ЧтениеXML.Имя + " = " + ЧтениеXML.Значение;
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстАтрибута);
			КонецЦикла;
		КонецЕсли;
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ЧтениеXML.Значение);
		КонецЕсли;
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю("/" + ЧтениеXML.Имя);
		КонецЕсли;
	КонецЦикла;
	ЧтениеXML.Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьЧерезDOM(Команда)
	ИмяФайла = Каталог + Формат(ДатаВыгрузки, "ДФ=dd.MM.yy") + ".xml";
	ЧтениеXML = Новый ЧтениеXML; //1
	ЧтениеXML.ОткрытьФайл(ИмяФайла);
	Построитель = Новый ПостроительDOM; //2
	Документ = Построитель.Прочитать(ЧтениеXML); //3
	Для Каждого Элемент Из Документ.ДочерниеУзлы Цикл //4
		Если Элемент.ИмяУзла = "КорневойУзел" Тогда
			Для Каждого Элемент1 Из Элемент.ДочерниеУзлы Цикл
				Если Элемент1.ИмяУзла = "Реализация" Тогда
					Для Каждого Элемент2 Из Элемент1.ДочерниеУзлы Цикл
						Сообщить(Элемент2.ТекстовоеСодержимое + Символы.ПС);
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	ЧтениеXML.Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Сериализация(Команда)
	СериализацияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Десериализация(Команда)
	ДесериализацияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СериализаторXDTO(Команда)
	СериализаторXDTOНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДесериализаторXDTO(Команда)
	ДесериализаторXDTOНаСервере();
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеОРеализацияхЗаДатуЦеликом()
	МассивДляВыгрузки = Новый Массив;       
	Запрос = Новый Запрос; 
	Запрос.УстановитьПараметр("ДатаНачала", ДатаВыгрузки); 
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ДатаВыгрузки));
	Запрос.Текст=
	"ВЫБРАТЬ
	|    РеализацияТоваровИУслуг.Сотрудник.Представление КАК СотрудникПредставление,
	|    РеализацияТоваровИУслуг.СуммаДокумента КАК СуммаДокумента,
	|    РеализацияТоваровИУслуг.ПризнакОплаты КАК ПризнакОплаты,
	|    РеализацияТоваровИУслуг.Дата КАК Дата,
	|    РеализацияТоваровИУслуг.Номер КАК Номер
	|ИЗ
	|    Документ.РеализацияТоваровИУслуг КАК РеализацияТоваровИУслуг
	|ГДЕ
	|    РеализацияТоваровИУслуг.Проведен
	|    И РеализацияТоваровИУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|УПОРЯДОЧИТЬ ПО
	|    Дата";  	
	Выборка = Запрос.Выполнить().Выбрать(); 
	Пока Выборка.Следующий() Цикл		
		МассивСтроки =  Новый Массив;
		МассивСтроки.Добавить(Выборка.СотрудникПредставление);
		МассивСтроки.Добавить(Выборка.СуммаДокумента);
		МассивСтроки.Добавить(Выборка.ПризнакОплаты);
		МассивСтроки.Добавить(Выборка.Дата);  
		МассивСтроки.Добавить(Выборка.Номер); 
		ДанныеСтроки = СтрСоединить(МассивСтроки, ";");       
		МассивДляВыгрузки.Добавить(ДанныеСтроки);
	КонецЦикла;   
	возврат МассивДляВыгрузки;
КонецФункции

&НаКлиенте
Процедура КаталогНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	ВыбратьКаталог("Выбор каталога для создания базы XML ", Новый ОписаниеОповещения("ПослеВыбораКаталогаДляВыгрузки", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКаталог(ЗаголовокДиалога, Оповещение)
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.Заголовок = ЗаголовокДиалога;
	Диалог.Показать(Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораКаталогаДляВыгрузки(ВыбранныеФайлы, Параметры) Экспорт
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Каталог = ВыбранныеФайлы[0] + "/";
КонецПроцедуры

&НаСервере
Процедура СериализацияНаСервере()
	ИмяФайла = Каталог + Формат(ДатаВыгрузки, "ДФ=dd.MM.yy") + ".xml";
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяФайла);
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ТипXML = XMLТипЗнч(РеализацияТоваровИУслуг);
	ЗаписьXML.ЗаписатьНачалоЭлемента(ТипXML.ИмяТипа, ТипXML.URIПространстваИмен);
	ЗаписьXML.ЗаписатьТекст(XMLСтрока(РеализацияТоваровИУслуг));
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.Закрыть();
КонецПроцедуры

&НаСервере
Процедура ДесериализацияНаСервере()
	ИмяФайла = Каталог + Формат(ДатаВыгрузки, "ДФ=dd.MM.yy") + ".xml";
	ОбъектСериализаторXDTO = Новый СериализаторXDTO(ФабрикаXDTO);
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ИмяФайла);
	ЧтениеXML.Прочитать();
	ТипЗначения = ИзXMLТипа(ЧтениеXML.ЛокальноеИмя, ЧтениеXML.URIПространстваИмен); //2
	ЧтениеXML.Прочитать();
	ПолученноеЗначение = XMLЗначение(ТипЗначения, ЧтениеXML.Значение); //3
	Сообщить(ПолученноеЗначение);
КонецПроцедуры

&НаСервере
Процедура СериализаторXDTOНаСервере()
	ИмяФайла = Каталог + Формат(ДатаВыгрузки, "ДФ=dd.MM.yy") + ".xml";
	НовыйСериализаторXDTO = Новый СериализаторXDTO(ФабрикаXDTO);
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяФайла);
	НовыйСериализаторXDTO.ЗаписатьXML(ЗаписьXML, РеализацияТоваровИУслуг);
	ЗаписьXML.Закрыть();
КонецПроцедуры

&НаСервере
Процедура ДесериализаторXDTOНаСервере()
	ИмяФайла = Каталог + Формат(ДатаВыгрузки, "ДФ=dd.MM.yy") + ".xml";
	НовыйСериализаторXDTO = Новый СериализаторXDTO(ФабрикаXDTO);
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ИмяФайла);
	РеализацияСсылка = НовыйСериализаторXDTO.ПрочитатьXML(ЧтениеXML);
	Сообщить(РеализацияСсылка);
	ЧтениеXML.Закрыть();
КонецПроцедуры